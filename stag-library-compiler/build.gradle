apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

compileJava {
    sourceCompatibility = "1.7"
    targetCompatibility = "1.7"
}

jacoco {
    toolVersion = jacoco_version
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport
jacocoTestReport.dependsOn test

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'com.google.testing.compile:compile-testing:0.12'
    testCompile project(path: ':sample-java-model')

    // Forcibly add test resources to test classpath: https://code.google.com/p/android/issues/detail?id=64887
    testRuntime files('build/resources/test')

    compile project(':stag-library')

    compile 'com.google.code.gson:gson:2.8.1'
    compile 'com.squareup:javapoet:1.8.0'
    compile 'com.intellij:annotations:12.0@jar'
    compile 'com.google.auto.service:auto-service:1.0-rc3'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}


group = ARTIFACTORY_GROUP
version = VERSION_NAME_STAG_COMPILER
status = STATUS

apply plugin: 'artifactory'
apply plugin: 'com.github.dcendents.android-maven'

configurations {
    published
}

task sourceJar(type: Jar) {
    from 'src/main/java'
    classifier "sources"
}

artifactoryPublish {
    dependsOn sourceJar
}

artifacts {
    published sourceJar
}

configure(install.repositories.mavenInstaller) {
    pom.project {
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
    }
}

artifactory {
    contextUrl = ARTIFACTORY_REPO_URL
    publish {
        repository {
            repoKey = REPO_KEY //The Artifactory repository key to publish to
            //username = artifactory_user //The publisher user name, property taken from gradle.properties
            //password = artifactory_password //The publisher password, property taken from gradle.properties
        }
        defaults {
            publishConfigs('archives', 'published')
            properties = ['build.status': "$it.project.status".toString()]
            publishPom = true //Publish generated POM files to Artifactory (true by default)
            publishIvy = false
            //Publish generated Ivy descriptor files to Artifactory (true by default)
        }
    }
    resolve {
        repository {
            repoKey = REPO_KEY //The Artifactory (preferably virtual) repository key to resolve from
            //username = artifactory_user //Optional resolver user name (leave out to use anonymous resolution), property taken from gradle.properties
            //password = artifactory_password //The resolver password, property taken from gradle.properties
        }
    }
}
